ext {
    PUBLISH_GROUP_ID = 'io.github.pknujsp'
    PUBLISH_VERSION = '0.0.1'
    PUBLISH_ARTIFACT_ID = 'smart-deeplink-navigation-safeargs'
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}
artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                pom {
                    name = 'smartdeeplink'
                    description = 'Smart deeplink for android navigation component'
                    url = 'https://pknujsp.github.io'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'pknujsp'
                            name = 'junsungPark'
                            email = "naversovc@gmail.com"
                            url = "https://github.com/pknujsp"
                        }
                    }

                    // Version control info
                    scm {
                        connection = 'scm:git:github.com/pknujsp/SmartDeepLink.git'
                        developerConnection = 'scm:git:ssh://github.com/pknujsp/SmartDeepLink.git'
                        url = 'https://github.com/pknujsp/SmartDeepLink/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"]
    )
    sign publishing.publications
}